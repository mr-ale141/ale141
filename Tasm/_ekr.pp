;───────── подпрограммы РИСОВАНИЯ в видеобуфере ───────────────────────────────
;
;call Line   ;ЛИНИЯ         ;  BX -адрес начала (будет адрес последней точки)
;                           ;  DX -направление: 2,-2,160,-160     AL -символ
;                           ;  CL -длина (кол-во точек)           AH -цвет
;
;call Ramka  ;РАМКА         ;  BX -адрес левого верха 
;                           ;  CH -размер по вертикали            AL  -символ
;                           ;  CL -размер по горизонтали          AH  -цвет
;
;call Box    ;ПРЯМОУГОЛЬНИК ;  BX -адрес левого верха 
;                           ;  CH -размер по вертикали            AL  -символ
;                           ;  CL -размер по горизонтали          AH  -цвет
;
;──────────────────────────────────────────────────────────────────────────────


;########################################################### тексты подпрограмм


;------ ЛИНИЯ 
          ; из BX -адрес начала (возвращается адрес последней точки)
          ; из DX -направление  (вправо 2, влево -2, вниз 160, вверх -160)
          ; из CL -длина (кол-во точек)
          ; из AL -символ:   '#' или 35 или 23h
          ; из AH -цвет:     14h  -синий фон, красный цвет 
          ;                  7Eh  -белый фон, желтый  цвет
LINE:
  push AX
  push CX
Line1:
  mov ES:[BX],AX   ;рисование "точки" (символ и цвет)
  add BX,DX        ;пересчет экранного адреса на DX
  dec CL           ;уменьшение счетчика
  jnz Line1        ;если линия не дорисована, то продолжать цикл
    sub BX,DX      ;шаг назад
  pop CX
  pop AX           
  ret


;------ РАМКА
          ; из BX  -адрес левого верха (возвращается он же)
          ; из CH  -кол-во строк рамки 
          ; из CL  -кол-во позиций рамки
          ; из AL  -символ рамки
          ; из AH  -фон и цвет
RAMKA:
  push CX
  push DX
             ;линия вправо (CL точек)
  mov DX,2         ;направление (+2)
  call Line        ;рисование линии
             ;линия вниз   (CH точек)
  xchg CH,CL       ;кол-во точек
  mov DX,160       ;направление (+160)
  call Line
             ;линия влево (CL точек)
  xchg CH,CL       ;кол-во точек
  mov DX,-2        ;направление (-2)
  call Line
             ;линия вверх (CH точек)
  xchg CH,CL       ;кол-во точек
  mov DX,-160      ;направление (-160)
  call Line

  pop DX
  pop CX
  ret

;------ ПРЯМОУГОЛЬНИК закрашенный
          ; из BX  -адрес левого верха (возвращается он же)
          ; из CH  -размер по вертикали   kY
          ; из CL  -размер по горизонтали kX  
          ; из AL  -символ рисования
          ; из AH  -фон и цвет рисования
BOX:
  push AX
  push BX
  push CX
  push DX
             ;рисование DH строк
  mov DH,CH         ;временный регистр для KY
Box1:                    ;--CH строк        
             ;рисование строки из DL позиций
  mov DL,CL         ;временный регистр для KX
  push BX           ;запомнить начало строки
Box2:                   
  mov ES:[BX],AX    ;символ и цвет в ячейки
  add BX,2          ;к следующей ячейке
  dec DL            ;уменьшение счетчика позиций
  jnz Box2
  pop BX            ;восстановиnm адрес начала строки

  add BX,160        ;на строку ниже
  dec DH            ;уменьшение счетчика строк
  jnz Box1 

  pop DX
  pop CX
  pop BX
  pop AX
  ret
