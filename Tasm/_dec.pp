;───────── подпрограммы для ДЕСЯТИЧНЫХ чисел ──────────────────────────────────
;
;call DecAL      ; печать 10-го числа из AL   (зона 4 позиции)
;
;call DecAX      ; печать 10-го числа из AX   (зона 8 позиций) 
;
;──────────────────────────────────────────────────────────────────────────────


;########################################################### тексты подпрограмм

;------ печать ДЕСЯТИЧНОГО числа из AL в 4 позициях:  "...3"  "..12"  ".255"
DecAL:
  push AX
  push CX
  push DX
              ;--формирование регистров CH,CL,DH (три десятичные цифры)
  mov AH,0          ;например, AL=234  (максимально три 10-ные цифры)
  mov DL,100     ;делить на 100
  div DL         ;старшая цифра: AL=2 и остаток: AH=34
  mov CH,AL      ;старшая цифра: CH=2
  mov AL,AH      ;остаток делить дальше
    mov AH,0
    mov DL,10    ;делить на 10
    div DL       ;средняя цифра: AL=3 и остаток AH=4
    mov CL,AL    ;средняя цифра: CL=3
    mov DH,AH    ;младшая цифра: DH=4    
  add CH,30h     ;старшая цифра в CH - '2' (номер символа на 30h больше цифры)
  add CL,30h     ;средняя цифра в CL - '3'
  add DH,30h     ;младшая цифра в DH - '4'
              ;--опробелить преднули в регистрах
  cmp CH,'0'     ;старшая цифра=0 ?
  jnz DecAL_1    ;если нет, то печать всех цифр
  mov CH,' '     ;если да, заменить '0' пробелом
    cmp CL,'0'   ;средняя цифра=0 ?
    jnz DecAL_1  ;если нет, то печать всех цифр
    mov CL,' '   ;если да, заменить '0' пробелом
              ;--печать цифр из регистров CH,CL,DH
DecAL_1:
  mov AL,' '     ;печать пробела 
  mov AH,0Eh         
  int 10h
    mov AL,CH    ;печать старшей цифры 
    int 10h
  mov AL,CL      ;печать средней цифры 
  int 10h
    mov AL,DH    ;печать младшей цифры
    int 10h
  pop DX   
  pop CX   
  pop AX
  ret


;------ печать ДЕСЯТИЧНОГО числа из AX в 8 позициях:  ".......3"  "...65535"
DecAX:                                                             
  push AX
  push BX
  push CX
  push DX
              ;--печать трех пробелов
  push AX
  mov AL,' '     
  mov AH,0Eh         
  int 10h
  int 10h
  int 10h
  pop AX
DecAX_1:
  mov CX,0       ;флаг: преднулей не было
  mov DX,0
  mov BX,10000   ;делитель 10000
  call Dec_pp
  mov BX,1000    ;делитель  1000
  call Dec_pp
  mov BX,100     ;делитель   100 
  call Dec_pp
  mov BX,10      ;делитель    10
  call Dec_pp
  mov BX,1       ;делитель     1
  call Dec_pp
DecAX_2:      ;--выход
  pop DX
  pop CX
  pop BX
  pop AX
  ret
         ;вспомог.п/п обработки и печати цифры
Dec_pp:
  div BX         ;целое в AX, остаток в DX
  add AL,30h     ;цифру в символ (номер символа на 30h больше цифры)
  cmp CX,0       ;флаг преднуля
  jnz Dec_pp1    ;преднули кончились
    mov CX,1     ;обработка преднулей
    cmp AL ,'0'
    jnz Dec_pp1
      mov AL,' ' ;пробел вместо 0
      mov CX,0
Dec_pp1:      ;--если AX=0, то при этом AL=' ' и BX=1
  cmp AL,' '   
  jnz Dec_pp2  
  cmp BX,1       ; если AL пробел
  jnz Dec_pp2    ; и если это единственная цифра,
    mov AL,'0'   ; то заменить пробел символом '0'
Dec_pp2:  
  mov AH,0Eh     ;печать цифры
  int 10h
  mov AX,DX      ;остаток как делимое
  mov DX,0       ;ст.половина делимого
  ret
