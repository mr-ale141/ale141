;─────────── ДВИЖЕНИЕ СИМВОЛА ─────────────────────────────────────────────────
; Символ движется клавишами со стрелками (с ожиданием нажатия), оставляя след
; Буква -смена следа.    Выход клавишей Esc.
 
  .model tiny 
  .code       
  org 100h    
  include _abc.mac
Start:      
  CLS               ;очистка экрана
  TEXT T1           ;печать подсказки о клавишах
  VIDEO             ;настройка ES на видеобуфер

  mov BX,12*160+80  ;начальное смещение на экране (строка 12, позиция 40)
  mov DX,0          ;направление: (Вправо +2, Влево -2, Вниз +160, Вверх -160)
  mov CH,'█'        ;символ, который двигается ("символ")
  mov CL,'·'        ;символ для следа          ("след")

                ;---рисование символа
Metka:              
  mov ES:[BX],CH    ;"символ" из CH в ячейку, номер которой в BX
            
  mov AH,8          ;опрос клавиатуры (нажатый символ в AL)
  int 21h   
        
  cmp AL,27         ;если нажато Esc, то выход
  jnz Metka0
    PAUSE
    STOP            ;завершение программы

Metka0:
  cmp AL,0          ;нажат спецкод ? (для спецкода AL=0 )
  jz Metka5         ;если спецкод, то опрос клавиатуры еще раз (какой спецкод)
    mov CL,AL       ;если буква, то смена "следа" в CL
    jmp Metka       ; и на рисование "символа"

                ;---формирование DX (направления) в зависимости от AL (клавиши)
Metka5:
  mov AH,8          ;какой же спецкод ? (второй опрос клавиатуры)
  int 21h   
                 
  cmp AL,75         ;влево ?
  jnz Metka1        ;если нет, то на следующее сравнение
    mov DX, -2      ;если да,  то направление в DX (влево)

Metka1:               
  cmp AL,77         ;вправо ?
  jnz Metka2          
    mov DX, 2       ;  направление вправо

Metka2:   
  cmp AL,80         ;вниз ?
  jnz Metka3              
    mov DX, 160     ;  направление вниз

Metka3:   
  cmp AL,72         ;вверх ?
  jnz Metka4              
  mov DX, -160      ;  направление вверх

                ;---действия после определения направления
Metka4:   
  mov ES:[BX],CL    ;стирание символа следом (он в CL)
  add BX,DX         ;пересчет  BX=BX+DX   (BX-текущий адрес   DX-направление)
    cmp BX, 160     ;выше  2 строки ?
    jc  Metka6
    cmp BX, 160*24  ;ниже 24 строки ?
    jnc Metka6
  jmp Metka         ;в пределах экрана (на рисование "символа")

                ;---шаг назад (упор на верхней и нижней границе экрана)
Metka6:
  sub BX,DX         ;пересчет  BX=BX-DX   (BX-текущий адрес   DX-направление)
  jmp Metka         ;на границах экрана (двигаться нельзя)

 T1 db  '   ESC -выход   СТРЕЛКИ -перемещение   БУКВА -смена следа  ', '$'
end Start
