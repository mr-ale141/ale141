;─────────── ГРАФИКА ──────────────────────────────────────────────────────────
; Точка, наклонная линия, рамка, два вложенных закрашенных прямоугольника

  .model tiny
  .code
  org 100h
  include _abc.mac
Start: 
  CLS                  ;макрос очистки экрана
  call GrafOn          ;включение графического режима
                       ;--точка
  mov CX, 50                ; X
  mov DX, 50                ; Y
  mov AL, 15                ;цвет
  call Pixel
                       ;--линия
  mov CX, 11                ; X начала линии
  mov DX, 11                ; Y начала линии
  mov SI, 2                 ; шаг по X
  mov DI, 6                 ; шаг по Y
  mov BX, 24                ;длина линии (кол-во точек)
  mov AL, 14                ;цвет
  call gLine
                       ;--рамка
  mov CX, 10                ; X левого верха
  mov DX, 10                ; Y левого верха
  mov SI, 620               ; размер по X (длина)
  mov DI, 460               ; размер по Y (высота)
  mov AL, 11                ;цвет
  call gRamka
                       ;--закрашенный прямоугольник
  mov CX, 60                ; X левого верха
  mov DX, 60                ; Y левого верха
  mov SI, 200               ; размер по X (длина)
  mov DI, 100               ; размер по Y (высота)
  mov AL, 12                ;цвет
  call gBox
                       ;--закрашенный прямоугольник (внутренний)
  add CX, 5                 ; X левого верха
  add DX, 5                 ; Y левого верха
  sub SI,  10               ; размер по X (длина)
  sub DI,  10               ; размер по Y (высота)
  mov AL, 2                 ;цвет
  call gBox

  PAUSE                ;макрос паузы
  call GrafOff         ;возврат в текстовый режим (или макросом "CLS")
  STOP                 ;макрос завершения программы


;============== ПОДПРОГРАММЫ для обработки графики (для 0 страницы) ==========

;──────────────────────────────────────────────────────────────────────────────
 ;== ВКЛЮЧЕНИЕ графического режима (номер режима из AL)
GrafOn:
    push AX
  mov AL,12h     ;Y от 0 до 480, X от 0 до 640, 16 цветов (цвет 0 -стирание)
  mov AH,0               
  int 10h
    pop AX
    ret

;──────────────────────────────────────────────────────────────────────────────
 ;== ВЫКЛЮЧЕНИЕ графического режима  (возврат в текстовый режим) 
GrafOff:          
    push AX
  mov AX,0003h   
  int 10h
    pop AX
    ret

;───────────────────┬────────────────────────────────┬─────────────────────────
 ;== ТОЧКА          │из CX  -координата X  (0..639)  │из AL  -цвет (1..15
 ;                  │из DX  -координата Y  (0..479)  │              0-стирание)
Pixel:             ;└────────────────────────────────┴─────────────────────────
    push AX
    push BX
  mov BH,0         ;страница 0
  mov AH,0Ch        
  int 10h
    pop BX
    pop AX
    ret

;────────────────┬────────────────────────┬─────────────────┬──────────────────
 ;== ЛИНИЯ       │из CX  -X начала линии  │из SI -шаг по X  │из BX -длина линии
 ;               │из DX  -Y начала линии  │из DI -шаг по Y  │из AL  -цвет линии
 ;               └────────────────────────┴─────────────────┴──────────────────
 ;            шаг по X,Y:  "1" -вправо,вниз   "-1" -влево,вверх  "0" -не изм-ся
 ;            координаты CX,DX будут на последней нарисованной точке
gLine:           
  push BX
gLine1:
  call Pixel       ;точка
  add CX,SI        ;пересчет координат на шаг
  add DX,DI        ;
  dec BX
  jnz gLine1
    sub CX,SI      ;шаг назад (на последнюю нарисованную точку)
    sub DX,DI      ;
  pop BX
  ret

;────────────────┬────────────────────────┬────────────────────┬──────────────────
 ;== РАМКА       │из CX  -X левого верха  │из SI -размер по X  │из AL  -цвет
 ;               │из DX  -Y левого верха  │из DI -размер по Y  │
 ;               └────────────────────────┴────────────────────┴──────────────────
 ;                    в CX,DX возвращает координаты левого верхнего угла
gRamka:
              ;-линия вправо (длина из SI, dX=1, dY=0)
  mov BX,SI      ;длина в BX
    push SI      ;сохранить размеры из SI,DI
    push DI      
  mov SI,1       ;шаг по Х =1  (вправо)
  mov DI,0       ;шаг по Y =0 
  call gLine     ;рисование линии (CX,DX будут на последней нарисованной точке)
    pop DI       ;восстановить размеры в SI,DI
    pop SI       
              ;-линия вниз (длина из DI, dX=0, dY=1)
  mov BX,DI      
    push SI
    push DI
  mov SI,  0       
  mov DI,  1       
  call gLine
    pop DI
    pop SI
              ;-линия влево (длина из SI, dX=-1, dY=0)
  mov BX,SI      
    push SI
    push DI
  mov SI, -1      
  mov DI,  0       
  call gLine
    pop DI
    pop SI
              ;-линия вверх (длина из DI, dX=0, dY=-1)
  mov BX,DI
    push SI
    push DI
  mov SI,  0
  mov DI, -1
  call gLine
    pop DI
    pop SI
    ret

;────────────────┬────────────────────────┬────────────────────┬──────────────────
 ;== ЗАКРАШЕННАЯ │из CX  -X левого верха  │из SI -размер по X  │из AL  -цвет
 ;   РАМКА       │из DX  -Y левого верха  │из DI -размер по Y  │
 ;               └────────────────────────┴────────────────────┴──────────────────
 ;                    в CX,DX возвращает координаты левого верхнего угла
gBox:
  push CX           ;запомнить координаты левого верха
  push DX
    push SI         ;запомнить размеры (портятся в цикле)
    push DI
gBox2:              ;внешний цикл:     -нарисовать DI линий
  push CX           ;  запомнить начало линии
  push SI           ;  запомнить размер по Х
gBox1:              ;внутренний цикл:  -нарисовать линию вправо из SI точек
    call Pixel      ;  точка ("X" из CX, "Y" из DX, цвет из AL)
    inc CX          ;  переход правее: X=X+1
    dec SI
    jnz gBox1
  pop CX            ;восстановить начало линии
  pop SI            ;восстановить размер по Х 
  inc DX            ;переход ниже: Y=Y+1
  dec DI
  jnz gBox2
    pop DI          ;восстановить размеры
    pop SI
  pop DX            ;восстановить в CX,DX координаты левого верха
  pop CX
  ret
;──────────────────────────────────────────────────────────────────────────────
end Start
