;─────────── МЫШЬ  ────────────────────────────────────────────────────────────
; Печатаются номер строки и позиции мыши (в текстовом режиме).
; Печатается название нажатой кнопки мыши. Выход нажатием любой клавиши
  
  .model tiny
  .code
  org 100h
  include _abc.mac     ;подключение библиотеки макросов
Start: 
  CLS                  ;макрос очистки экрана
; VIDEO                ;макрос для доступа к видеобуферу

                 ;== РАЗМЕР черточки курсора и ВИДИМОСТЬ куpсоpа
  mov CH, 32           ;верх черточки: 0..31   32 -не показывать курсор
  mov CL, 31           ;низ  черточки: 0..31
  mov AH,1            
  int 10h

  TEXT Tt              ;постоянный поясняющий текст
; call MouseInit       ;--инициализация мыши
  call MouseOn         ;--показать мышь
Metka:
                       ;--опрос клавиатуры без приостановки 
  mov AH,11h               ;нажато?  (если не нажато, то флаг нуля 1)
  int 16h    
  jnz Metka0               ;если нажато (т.е.флаг нуля=0), то выход из цикла

  call MousePar        ;--взятие параметров мыши:   в CX -"X", в DX -"Y" 
                          ;  в BX -кнопки (0 -не нажаты, 1 -левая, 2 -правая)
  KURSOR 0,7           ;--печать координат мыши
  mov AX,DX
  call DecAL              ;печать координаты Y (графической)
    KURSOR 1,7
    mov AX,CX
    call DecAL            ;печать координаты Х (графической)
                       ;--печать названия нажатой книпки
  lea DX,T0               ;текст "не нажато"
  cmp BX,0
  jz Metka1
    lea DX,T1             ;текст "левая"
    cmp BX,1
    jz Metka1
  lea DX,T2               ;текст "правая"
  cmp BX,2
  jz Metka1
    lea DX,T3             ;текст "обе"
Metka1:
  KURSOR 2,12
  STRING                  ;печать состояния кнопок (текст с адресом из DX)
  jmp Metka

Metka0:
  call MouseOff        ;--скрыть мышь
  PAUSE                ;макрос паузы
  STOP                 ;макрос завершения программы
 include _dec.pp       ;подключение библиотеки подпрограмм печати 10-ных чисел

 Tt db "Строка  ",13,10
    db "Позиция ",13,10
    db "Кнопки: ",'$'
 T0 db "не нажаты",'$'
 T1 db " ЛЕВАЯ   ",'$'
 T2 db "  ПРАВАЯ ",'$'
 T3 db "   ОБЕ   ",'$'


;============== ПОДПРОГРАММЫ для обработки мыши ===============================

;──────────────────────────────────────────────────────────────────────────────
 ;== ПОКАЗАТЬ мышь (сделать видимой)
MouseOn:
    push AX
  mov AX,1
  int 33h
    pop AX
    ret

;──────────────────────────────────────────────────────────────────────────────
 ;== СКРЫТЬ мышь   (сделать невидимой)
MouseOff:
    push AX
  mov AX,2
  int 33h
    pop AX
    ret

;──────────────────────────────────┬───────────┬───────────────────────────────
;== Определение КООРДИНАТ мыши     │в CX -"x"  │в BX -кнопки: 0 -не нажаты
;    (текстовых: x=0..24 y=0..79)  │в DX -"y"  │              1 -нажата левая
;   и состояния КНОПОК             │           │              2 -нажата правая
MousePar:
    push AX
  mov AX,3
  int 33h
      and BX,3    ;номер нажатой кнопки (0-не нажато, 1-левая, 2-правая, 3-обе)
    shr CX,1      ; X = X div 8 (вместо сдвигов вправо можно команду "div")
    shr CX,1      ;координата текстовая = координата графическая div 8
    shr CX,1      
  shr DX,1        ; Y = Y div 8
  shr DX,1
  shr DX,1
    pop AX
    ret

;──────────────────────────────────────────────────────────────────────────────
 ;== ИНИЦИАЛИЗАЦИЯ мыши (не обязательно)
MouseInit:
                     ;--инициализация мыши
  mov AX,0           
  int 33h
                     ;--установка номера экранной страницы
  mov BX, 0             ;экранная страница
  mov AX,1Dh         
  int 33h
                     ;--установка диапазона перемещения по X
  mov CX, 0             ;минимум
  mov DX, 640           ;максимум (80*8)
  mov AX,7           
  int 33h
                     ;--установка диапазона перемещения по Y
  mov CX, 0             ;минимум
  mov DX, 200           ;максимум (25*8)
  mov AX,8           
  int 33h
                     ;--начальная установка мыши
  mov CX, 20            ; X
  mov DX, 10            ; Y
  mov AX,4           
  int 33h
  ret

end Start

