;─────────── МЕНЮ ─────────────────────────────────────────────────────────────
; По меню перемещается рамка клавишами "Вверх" и "Вниз"    -с упором
;                          и клавишами "Влево" и "Вправо"  -по кругу
; При нажатии Enter печатается название выбранного пункта
; Выход - клавишей Esc

  .model tiny
  .code
  org 100h
  include _abc.mac
Start:
  CLS
  VIDEO
                  
  call Copy       ;копировать меню из текста на экран
  mov SI, Adres   ;в "SI" -экранный адрес текущего (первого) пункта меню

Metka0:
  KURSOR 0,0
  TEXT help       ;подсказка о клавишах
Metka:
  mov DH, Cwet    ;показать рамку (перекрасить текущий пункт меню цветом Cwet)
  call Kras       
                  
  mov AH,10h      ;опрос клавиатуры (код в "AL", спецкод в "AH")
  int 16h
                  
  mov DH, Cwet0   ;убрать рамку (перекрасить текущий пункт меню цветом Cwet0)  
  call Kras       

    ;-----------анализ нажатого
  cmp AL,27       ;если нажато "Esc" -выход
  jnz Metka1       
    PAUSE                        ;выход
    STOP

    ;------------ВНИЗ
Metka1:
  cmp AH, 80      ;если нажато "Вниз"  - рамку вниз (до упора)
  jnz Metka2
    add SI, 160                  ;пересчитать адрес пункта вниз
    cmp SI, Adres+Kol*160        ;сравнить с адресом конечного пункта
    jc Metka                     ;если еще в меню, то в начало цикла
      sub SI, 160                ;если вышли за меню, то шаг назад
      jmp Metka                  ; и на начало цикла

Metka2:
  cmp AH, 77      ;если нажато "Вправо"  - рамку вниз (по кругу)
  jnz Metka3
    add SI, 160                  
    cmp SI, Adres+Kol*160  
    jc Metka               
      mov SI, Adres              ;если вышли за меню, то SI -на первый пункт
      jmp Metka                

    ;------------ВВЕРХ
Metka3:
  cmp AH, 72      ;если нажато "Вверх" - рамку вверх (до упора)
  jnz Metka4
    sub SI, 160                  ;пересчитать адрес пункта вверх
    cmp SI, Adres                ;сравнить с адресом начального пункта
    jnc Metka                    ;если еще в меню, то в начало цикла
      add SI, 160                ;если вышли за меню, то шаг назад
      jmp Metka                  ; и на начало цикла

Metka4:
  cmp AH, 75      ;если нажато "Влево" - рамку вверх (по кругу)
  jnz Metka5
    sub SI, 160         
    cmp SI, Adres       
    jnc Metka           
      mov SI,Adres+Kol*160-160   ;если вышли за меню, то SI -на последний пункт
      jmp Metka                

    ;------------ВЫПОЛНИТЬ пункт
Metka5:
  cmp AL,13       ;если нажато "Enter" - скопировать из рамки в другое место
  jnz Metka            ;на начало цикла
    mov DH, 0Ch        ;перекрасить текущий пункт меню цветом 12
    call Kras       
                       ;скопировать с адреса ES:[SI]  (из рамки) "Len" символов
                       ;            в адрес  ES:[BX]  (в верхнюю строку)
    push SI               ;запомнить адрес начала пункта
    mov BX, 0*160+72*2    ;куда копировать (строка 0, позиция 72)
    mov CX, Len           ;сколько символов копировать
  Metka6:
    mov AL,      ES:[SI]  ;взять с экрана
    mov ES:[BX],   AL     ;копировать в верхнюю строку
    mov ES:[BX+1], DH     ;перекрасить в верхней строке
    add SI,2              ;пересчет адреса в меню
    add BX,2              ;пересчет адреса в верхней строке
    dec CX                ;выполнить CX раз
    jnz Metka6
    pop SI                ;восстановить адрес начала пункта

    PAUSE
  jmp Metka0      ;на начало цикла (на подсказку)
                  

;──────────────────────────────────────────────────────────────────────────────
                        ;-цвет пунктов меню
  Cwet  db 4Eh              ;цвет в рамке
  Cwet0 db 0Ah              ;цвет без рамки
                        ;-текст пунктов меню (одиноковой длины)
  T1 db  " New  "        
     db  " Load "
     db  " Save "
  T2 db  " Edit "
                        ;-длина и кол-во пунктов
  Len =   $ - T2            ;длина каждого пункта меню
  Kol = ( $ - T1) / Len     ;кол-во пунктов меню (длина всех пунктов, деленная
                            ;                     на длину одного пункта)
                        ;-координаты меню на экране
   Y=12                     ; Y - от 0 по 25-Kol
   X=20                     ; X - от 0 по 80-Len
  Adres = Y*160 + X*2       ;адрес меню на экране

;──────────────────────────────────────────────────────────────────────────────
  ;--п/п КОПИРОВАНИЯ "Kol" пунктов меню длиной по "Len" символов каждый
  ;                   из переменной "T1" в область на экране "Adres"
  ;                     (как двумерный массив символов, т.е. массив строк)
Copy:
  mov SI, Adres      ;адрес меню на экране
  mov BX, offset T1  ;адрес переменной текста меню
  mov DI, Kol        ;кол-во строк меню 
Copy1:
    push BX          ;сохранить начало текста
    push SI          ;сохранить начало строки экрана
  mov AH, Cwet0      ;каким цветом
  mov CX, Len        ;сколько символов копировать
Copy2:
  mov AL, [BX]       ;взять символ из переменной
  mov ES:[SI], AL    ;копировать его на экран
  mov ES:[SI+1], AH  ;цвет
  inc BX             ;к следующему символу переменной
  add SI,2           ;к следующей позиции экрана
  dec CX
  jnz Copy2
    pop SI           ;восстановить начало строки экрана
    add SI,160       ;перейти на начало следующей строки экрана
    pop BX           ;восстановить начало текста
    add BX, Len      ;перейти на начало следующей строки текста
  dec DI
  jnz Copy1
  ret

;──────────────────────────────────────────────────────────────────────────────
  ;--п/п ПЕРЕКРАСКИ текущего пункта меню на экране
  ;                 цвет из "DH", длина из "Len", адрес на экране из "SI"
Kras:
    push SI
  mov CX, Len        ;сколько символов красить
Kras1:
  mov ES:[SI]+1,DH   ;перекрасить символ
  add SI,2           ;к следующей позиции экрана 
  dec CX             ;перекрасить Len символов
  jnz Kras1
    pop SI
  ret

  Help db " ESC-выход  ВВЕРХ/ВНИЗ-с упором  ВЛЕВО/ВПРАВО-по кругу  ENTER-выполнить       ",'$'
end Start
